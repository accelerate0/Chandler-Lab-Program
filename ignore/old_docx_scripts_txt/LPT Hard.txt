\ Variable Interval (30s) Schedule for lever press delivery of sucrose pellets combined with
\ 9 total Tone-Shock pairings in 3 pairing blocks each separated by a fixed 5 min inter block interval and each
\ tone shock pairing is separated by a variable 3min inter trial interval based on Greg Quirk's Platform-Mediated Avoidance Task

\Response = Lever press (or nose poke)
\Reinforcer = Pellets dispensed on first response after VI-30


\-------------------------------------------------------------------------\

\***********************
\     INPUTS
\***********************


^RightLever = 1


^Magazine = 4
                                    
                                                                                                                         




                                               
\***********************
\     OUTPUTS
\***********************


^RightOut = 1
   
   
^LeverLight = 2
    
                                                                                                                 
^Pellet = 3


^Shock = 4


^RightLight = 5

                                                                                                                 
^Tone = 6

                                                  
^HouseLight = 7


^TTLTone = 9
    
                                          
^TTLL = 10


\***********************                                                                        
\     CONSTANTS
\***********************


^SessionLength = 60 \length in minutes


^VISchedule = 30 \Variable interval schedule with mean interval of 30sec


^CorrectResponse = 3 \Right lever press following the end of the VI timer


^ITI = 180 \Mean Intertrial interval in seconds


^Shutdown = 12 \


\***********************
\     DATA - TOTALS
\***********************

DIM A = 6


DIM B = 1 \SHOCK ARRAY - ARRAY MUST BE 0x0 so can be used in ZEROARRAY FUNCTION


\A(0) = Lever Presses


\A(1) = Pellets delivered


\A(2) = Magazine Entries


\A(3) = Tone counter


\A(4) = InterBlock Interval counter


\A(5) = Shock counter


\***********************
\     TIMESTAMPS
\***********************


DIM C = 1

DIM D = 5000 \ ARRAY OF TIMESTAMPS FOR PELLET DELIVERIES

DIM E = 5000 \ ARRAY OF TIMESTAMPS FOR LEVER PRESSES

DIM F = 5000 \ ARRAY OF TIMESTAMPS FOR PORT ENTRIES

DIM G = 5000 \ ARRAY OF TIMESTAMPS FOR TONE ONSET

DIM H = 5000 \ ARRAY OF TIMESTAMPS FOR SHOCK ONSET

DIM J = 5000 \ ARRAY OF TIMESTAMPS FOR INTER BLOCK INTERVAL ONSET


\********************************
\    SESSION VARIABLES
\********************************

DIM X = 3


\X(0) = VI Schedule timer


\X(1) = Session timer


\X(2) = ITI timer




\***********************
\    LISTS
\***********************


\VI OPTION LIST - This list will be initialized into a VI schedule

                         
LIST Y = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10
                      


\ITI OPTION LIST - This list of options will be initialized into a ITI timer


LIST I = 1, 2, 3, 4, 5


\***************************
\     DATA MANAGEMENT
\***************************


DISKVARS = A, C, D, E, F, G, H, J, X, Y, I


DISKOPTIONS = FULLHEADERS


\-------------------------------------------------------------------------

\**************************************
\    State Set 1 - Program Setup
\**************************************


S.S.1,

S1, \Loading the program shows the session length and VIschedule and seals all the arrays

    0.01": SHOW 1, SessionLength, ^SessionLength; SHOW 2, Mean VI, ^VISchedule; SHOW 3, ITI, ^ITI ---> S2  \SHOW KEY VARIABLES


S2,\Starting the program turns on the houselight, inserts the right lever

    #START: ON ^RightLight; ON ^RightOut ---> S3


S3, \State 3 is the session timer, after set time elapses the session is stopped and data is saved

    1': ADD X(1); SHOW 1, Session timer, X(1); If X(1)=60 [@TRUE, @FALSE]
        @TRUE: ---> STOPABORTFLUSH
        @FALSE: ---> SX

\-------------------------------------------------------------------------

\************************************************************************************
\     State Set 2 - VI timer, pellet dispensing, and reward count and display
\************************************************************************************


S.S.2,


S1, \State 1 responds to the start command, establishing VI schedule timer
                                                                                                      

#START: INITCONSTPROBARR Y, ^VISchedule;


RANDI X(0) = Y; SET X(0) = X(0) * 1"; \Multiplying by 1 second to get array into units of seconds

                                                                                                       
SHOW 3, Pellets delivered, A(1) ---> S2





S2, \ State 2 waits to make a pellet available


X(0)#T: ---> S3





S3, \ State 3 waits for a lever press following VI time to deliver a pellet


#Z^CorrectResponse: ON ^Pellet; ON ^LeverLight; ADD A(1); SET D(A(1)) = C(1);

SHOW 3, Pellets delivered, A(1); SET D(A(1)+1) = -987.987 ---> S4




S4, \ State 4 turns off the pellet dispenser, resets the VI timer, and sends to state that waits for timer to elapse


0.5": OFF ^Pellet; OFF ^LeverLight; RANDI X(0) = Y; SET X(0) = X(0) * 1" ---> S2





S5, \ State 5 is a holding state


#START: ---> SX




\-------------------------------------------------------------------------

\**************************************
\    State Set 3 - Levers
\**************************************


S.S.3,


S1,\State 1 displays right lever presses

#START: SHOW 2, Lever Presses, A(0) ---> S2


S2, \State 2 responds to right lever presses


#R^RightLever: ADD A(0); SET E(A(0)) = C(1); SHOW 2, Lever Presses, A(0); SET E(A(0)+1) = -987.987;


IF S.S.2 = 3 [@REWARD, @NONE]  \If the VI has elapsed and the animal has pressed the lever trigger reward delivery


@REWARD: Z^CorrectResponse ---> SX


@NONE: ---> SX


#Z^Shutdown: ---> S3


S3,\ S3 is a holding state


#START: ---> SX


\-------------------------------------------------------------------------

\******************************************
\    State Set 4 - Magazine Entries
\******************************************


S.S.4,


S1,\State 1 responds to the start command


#START: SHOW 4, Magazine Entries, A(2) ---> S2





S2,\State 2 records magazine entries


#R^Magazine: ADD A(2); SET F(A(2)) = C(1); SHOW 4, Magazine Entries, A(2); SET F(A(2)+1) = -987.987 ---> SX




S3,\State 3 is a holding state


#START: ---> SX


\-------------------------------------------------------------------------

\********************************************************
\        State Set 5 - Event Clock
\    Allows for Timestamping with .01 sec resolution
\********************************************************


S.S.5,

S1,

#START: ---> S2

S2,

0.01": SET C(1) = C(1) + 0.01 ---> SX

\-------------------------------------------------------------------------

           
